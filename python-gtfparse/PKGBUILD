# Maintainer: Clint Valentine <valentine.clint@gmail.com>

_name=gtfparse
pkgbase='python-gtfparse'
pkgname=('python-gtfparse' 'python2-gtfparse')
pkgver=1.1.2
pkgrel=1
pkgdesc="Python library for parsing GTF files"
arch=('any')
url="https://pypi.python.org/pypi/gtfparse"
license=('Apache')
makedepends=(
  'python' 'python-setuptools'
  'python2' 'python2-setuptools')
options=(!emptydirs)
source=("${pkgname}"-"${pkgver}".tar.gz::https://pypi.io/packages/source/"${_name:0:1}"/"${_name}"/"${_name}"-"${pkgver}".tar.gz)
sha256sums=('c7ce355e79c2b9f81d2c7da048c4d9d1516023fa1781212ebeefb21e470d5076')

prepare() {
  cp -a "${_name}"-"${pkgver}"{,-py2}
}

build(){
  cd "${srcdir}"/"${_name}"-"${pkgver}"
  python setup.py build

  cd "${srcdir}"/"${_name}"-"${pkgver}"-py2
  python2 setup.py build
}

package_python2-gtfparse() {
  depends=('python2' 'python2-numpy' 'python2-pandas')

  cd "${_name}"-"${pkgver}"-py2
  python2 setup.py install --root="${pkgdir}"/ --optimize=1 --skip-build
}

package_python-gtfparse() {
  depends=('python' 'python-numpy' 'python-pandas')

  cd "${_name}"-"${pkgver}"
  python setup.py install --root="${pkgdir}"/ --optimize=1 --skip-build
}
